#!/bin/zsh

##
## Блок отображения и управления уровнем громкости.
## Выводит индикатор текущего уровня громкости в виде иконки и числового значения.
## Перехватывает следующие события мыши:
##   * нажатие левой кнопки - открывает окно pavucontrol
##   * нажатие средней кнопки - включает/выключает звук текущего канала по умолчанию
##   * нажатие правой кнопки - делает каналом по умолчанию следующий канал по списку
##     и переводит все источники звука на новый канал
##   * вращений колесика вверх - увеличивает громкость канала по умолчанию на 5%
##   * вращений колесика вниз - уменьшает громкость канала по умолчанию на 5%
##

change_sink () {
    # Получаем текущий sink по умолчанию
    local def_sink=$(pactl info | grep -oP "(?<=Default Sink: ).+")

    local OLD_IFS=$IFS
    IFS=$'\n'
    for line in $(pactl list sinks | pcre2grep -M -o1 -o2 --om-separator=: 'Name: (.*)\n\tDescription: (.*)\n'); do
        # Запоминаем предыдущий sink
        local prev_sink=("${cur_sink[@]}")
        # Переводим текущую строку в массив по разделителю :
        local cur_sink=(${(s/:/)line})
        # Запоминаем начальный sink
        [ ! -n "$first_sink" ] && local first_sink=("${cur_sink[@]}")
        # Если sink на предыдущей итерации - тот, который сейчас по умолчанию, то мы нашли новый sink по умолчанию
        [ "$prev_sink[1]" = "$def_sink" ] && local new_def_sink=("${cur_sink[@]}")
    done
    IFS=$OLD_IFS

    # Если не определили новый sink по умолчанию, то значит им будет первый
    if [ ! -n "$new_def_sink" ]; then
        local new_def_sink=("${first_sink[@]}")
    fi
    pactl set-default-sink "$new_def_sink[1]" >/dev/null 2>&1
    pgrep -x dunst >/dev/null && notify-send -i gnome-volume-control "Новый канал по умолчанию" "$new_def_sink[2]"

    # Перенаправляем все источники звука на новый sink по умолчанию
    for input in $(pactl list short sink-inputs | awk '{print $1}'); do
        pactl move-sink-input "$input" "$new_def_sink[1]"
    done
}


case $BLOCK_BUTTON in
    1) setsid pavucontrol ;;
    2) pactl set-sink-mute @DEFAULT_SINK@ toggle ;;
    3) change_sink ;;
    4) pactl set-sink-volume @DEFAULT_SINK@ +5% ;;
    5) pactl set-sink-volume @DEFAULT_SINK@ -5% ;;
esac


# Определение текущего уровня звука
local icon=""
if [ "$(pulsemixer --get-mute)" = "1" ]; then
    # Если канал заглушен
    icon=""
    local text="<span color=\"#D01B24\"></span>"
else
    local vol=$(pulsemixer --get-volume | awk '{print $1}')
    [ "$vol" -gt "50" ] && icon=
    local text=${vol}%
fi

echo "<span color=\"#61B8D0\">${icon}</span> ${text}"
